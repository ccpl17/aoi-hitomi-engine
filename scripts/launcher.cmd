@echo off

rem ä½¿ç”¨ UTF-8 å­—å…ƒç·¨ç¢¼
    chcp 65001
    cls

@rem æ¨™é¡Œ
set title=è—çœ¼å¼•æ“å•Ÿå‹•å™¨ Aoi Hitomi Engine Launcher
    title %title%

@rem æ‡‰ç”¨ç¨‹å¼åç¨±
set app_name=aoi-hitomi-engine

@rem æ‡‰ç”¨ç¨‹å¼ç‰ˆæœ¬
set app_version=24.12.0

@rem Python ç‰ˆæœ¬
set python_version=3.12.7

rem å‰–æ Python æ¬¡è¦ç‰ˆæœ¬
    for %%v in (%python_version%) do (
        for /f "delims=. tokens=1-3" %%a in ("%%v") do (
            set python_minor_version=%%b
        )
    )

rem è¨­å®š Unicode æ§åˆ¶å­—å…ƒ
set ESC=

rem è¨­å®šæ–‡å­—æ¨£å¼
set default=%ESC%[0m
set red=%ESC%[91m
set green=%ESC%[92m
set blue=%ESC%[94m
set blue_background=%ESC%[44m
set white=%ESC%[97m

rem è¨­å®šç®­é ­åŠå…¶æ¨£å¼
set "arrow_long_right===^>"
set "arrow_green=%green%%arrow_long_right%"
set "arrow_blue=%blue%%arrow_long_right%"

rem è¨­å®šæ ¹ç›®éŒ„ä½ç½®
set root=%cd%

rem è¨­å®šæ‡‰ç”¨ç¨‹å¼äºŒé€²ä½æª”æ¡ˆç›®éŒ„ä½ç½®
set bin=%root%\bin

rem è¨­å®šä¸»ç¨‹å¼ä½ç½®
set main_program=%bin%\ahe.pyc

rem è¨­å®šåŸ·è¡Œç’°å¢ƒç›®éŒ„ä½ç½®
set runtime=%root%\runtime

rem è¨­å®š Python åŸ·è¡Œç’°å¢ƒç›®éŒ„ä½ç½®
set python_runtime=%runtime%

rem è¨­å®š Python ç›´è­¯å™¨ä½ç½®
set python=%python_runtime%\python.exe

rem è¨­å®š Standalone ZIP Application for pip ä½ç½®
set pip_pyz=%python_runtime%\pip.pyz

rem è¨­å®š Python æ›é‰¤æª”æ¡ˆä½ç½®
set python_pth=%python_runtime%\python3%python_minor_version%._pth

rem è¨­å®šæª”æ¡ˆåç¨±ï¼šEmbeddable Python for Windows å£“ç¸®æª”
set embeddable_python_for_windows_zip=python-%python_version%-embed-amd64.zip

@rem ç¨‹å¼é€²å…¥é»
:main

rem é¡¯ç¤ºæ‡‰ç”¨ç¨‹å¼è³‡è¨Š
    echo %blue_background% %title% %default%
    echo.

@rem æª¢æŸ¥ Python åŸ·è¡Œç’°å¢ƒ
:check_python_runtime
    echo %arrow_green% %white%æª¢æŸ¥ Python åŸ·è¡Œç’°å¢ƒ%default%
    echo.

    if exist %python% (
rem å¦‚æœ Python å­˜åœ¨å‰‡å•Ÿå‹•æ‡‰ç”¨ç¨‹å¼
        goto :bootstrap_application
    ) else (
rem å¦‚æœ Python ä¸å­˜åœ¨å‰‡åˆå§‹åŒ– Python åŸ·è¡Œç’°å¢ƒ
        echo %red%è­¦å‘Š%white%: Python åŸ·è¡Œç’°å¢ƒä¸å­˜åœ¨%default%
        echo.
        goto :initialize_python_runtime
    )

@rem åˆå§‹åŒ– Python åŸ·è¡Œç’°å¢ƒ
:initialize_python_runtime
    echo %arrow_green% %white%åˆå§‹åŒ– Python åŸ·è¡Œç’°å¢ƒ%default%
    echo.

rem ä¸‹è¼‰ Embeddable Python for Windows
    echo %arrow_green% %white%æ­£åœ¨ç²å– %green%Embeddable Python %python_version% for Windows%default%
    echo.
    echo %arrow_blue% %white%æ­£åœ¨ä¸‹è¼‰ https://www.python.org/ftp/python/%python_version%/%embeddable_python_for_windows_zip%%default%
    call curl --create-dirs --output-dir %python_runtime% --progress-bar --remote-name --url https://www.python.org/ftp/python/%python_version%/%embeddable_python_for_windows_zip%
    echo.

rem è§£å£“ç¸® Embeddable Python for Windows
    echo %arrow_blue% %white%æ­£åœ¨è§£å£“ç¸® %runtime%\%embeddable_python_for_windows_zip%%default%
    call tar -xf %python_runtime%\%embeddable_python_for_windows_zip% -C %python_runtime%
    echo.

rem "åˆªé™¤ Embeddable Python for Windows å£“ç¸®æª”"
    echo %arrow_blue% %white%æ­£åœ¨åˆªé™¤ %runtime%\%embeddable_python_for_windows_zip%%default%
    del %python_runtime%\%embeddable_python_for_windows_zip%
    echo.

rem å•Ÿç”¨ Python çš„ site æ›é‰¤
    echo %arrow_blue% %white%æ­£åœ¨å•Ÿç”¨ Python çš„ site æ›é‰¤%default%
    echo python3%python_minor_version%.zip > %python_pth%
    echo .>> %python_pth%
    echo.>> %python_pth%
    echo # Uncomment to run site.main() automatically>> %python_pth%
    echo import site>> %python_pth%
    echo.

rem ä¸‹è¼‰ Standalone ZIP Application for pip
    echo %arrow_green% %white%æ­£åœ¨ç²å– %green%Standalone ZIP Application for pip%default%
    echo.
    echo %arrow_blue% %white%æ­£åœ¨ä¸‹è¼‰ https://bootstrap.pypa.io/pip/pip.pyz%default%
    call curl --create-dirs --output-dir %python_runtime% --progress-bar --remote-name --url https://bootstrap.pypa.io/pip/pip.pyz
    echo.

rem å®‰è£ setuptools
    echo %arrow_blue% %white%æ­£åœ¨å®‰è£ setuptools%default%
    call %python% %pip_pyz% install --no-cache-dir setuptools
    echo.

rem å®‰è£å•Ÿå‹•æ‡‰ç”¨ç¨‹å¼æ‰€éœ€è¦çš„æ‰€æœ‰ Python å¥—ä»¶
    echo %arrow_blue% %white%æ­£åœ¨å®‰è£å•Ÿå‹•æ‡‰ç”¨ç¨‹å¼æ‰€éœ€è¦çš„æ‰€æœ‰ Python å¥—ä»¶%default%
    call %python% %pip_pyz% install --no-warn-script-location --no-cache-dir pywebview rich tomlkit win32more
    echo.

rem è§£é™¤å®‰è£ setuptools
    echo %arrow_blue% %white%æ­£åœ¨è§£é™¤å®‰è£ setuptools%default%
    call %python% %pip_pyz% uninstall --yes setuptools
    echo.

rem åˆªé™¤ Standalone ZIP Application for pip
    echo %arrow_blue% %white%æ­£åœ¨åˆªé™¤ Standalone ZIP Application for pip%default%
    del %pip_pyz%
    echo.
    
@rem å•Ÿå‹•æ‡‰ç”¨ç¨‹å¼
:bootstrap_application
    echo %arrow_green% %white%æ­£åœ¨å•Ÿå‹• %green%%app_name% %app_version%%default%
    echo.
    call %python% %main_program%

pause
